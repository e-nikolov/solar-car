%------------------------- Sort declarations -------------------------------------%


sort SolarCarState = struct On ? isOn | Off ? isOff;
sort BatteryLevelStatus = struct Empty ? isEmpty | Medium ? isMedium | Full ? isFull;
sort BatteryTempStatus  = struct Overheating ? isOverheating | Normal ? isNormal;

%------------------------- Map declarations -------------------------------------%

map MAX : Int;
eqn MAX = 5;

map SPEED_THRESHOLD : Int;
eqn SPEED_THRESHOLD = 3;


%------------------------- Action declarations -------------------------------------%

% Input actions

act pressOn, pressOff;
    pressThrottle : Int;
    releaseThrottle;
    pressBrake, releaseBrake;

    activateCruise : Int;
    deactivateCruise;
    emergencySignal;

% Battery actions

    getBatteryLevel : BatteryLevelStatus;
    getBatteryTemp : BatteryTempStatus;
    conBatteryToMotor : Int;

% Motor actions

    getSpeed : Int;
    conMotorToBattery : Int;
    setMotorRequirePower : Int;

% Solar Panel actions
    getSolarPower : Int;
    conSolarPanelToMotor : Int;
    conSolarPanelToBattery : Int;

% Mechanical brakes

    activateBrakes, deactivateBrakes;

% External environment/external hazard

    enterSafeMode, exitSafeMode;

% Monitoring system

    sendBatteryLevel : BatteryLevelStatus;
    sendBatteryTemp : BatteryTempStatus;
    sendSolarPower : Int;
    sendSpeed : Int;

% Internal actions

    sendStreamDataStart, receiveStreamDataStart, startStreamData;
    sendStreamDataEnd, receiveStreamDataEnd, endStreamData : Int # BatteryLevelStatus # BatteryTempStatus # Int;

    sendCruiseStepStart, receiveCruiseStepStart, startCruiseStep : Int # Int # Int # BatteryLevelStatus # BatteryTempStatus # Int;
    sendCruiseStepEnd, receiveCruiseStepEnd, endCruiseStep : Int;

    sendManagePowerStart, receiveManagePowerStart, startManagePower : Int # Int # BatteryLevelStatus # BatteryTempStatus # Int;
    sendManagePowerEnd, receiveManagePowerEnd, endManagePower;

    continue;

    test : Bool;
    testN : Int;
    newCycle : Bool # Bool # Int # Int;


%------------------------- Process declarations -------------------------------------%

%------------------------ MainController Processes --------------%


proc MainController(on : Bool, cruise : Bool, throttle : Int, desiredSpeed : Int) = 
    (on)
    ->  (
            newCycle(on, cruise, throttle, desiredSpeed)
            . sendStreamDataStart 
            . sum speed : Int, batteryLevel : BatteryLevelStatus, batteryTemp : BatteryTempStatus, solarPower : Int
            . receiveStreamDataEnd(speed, batteryLevel, batteryTemp, solarPower)
            . 
            (
                (batteryLevel == Full || batteryTemp == Overheating) 
                ->  conMotorToBattery(0) . conSolarPanelToBattery(0)
                <>  continue
            )
            .
            (
                (cruise == true)
                ->  sendCruiseStepStart(throttle, desiredSpeed, speed, batteryLevel, batteryTemp, solarPower)
                    . sum newThrottle : Int . receiveCruiseStepEnd(newThrottle)
                    . MainController(true, true, newThrottle, desiredSpeed)

                + (throttle > 0)
                ->  releaseThrottle . deactivateCruise
                    . sendManagePowerStart(0, speed, batteryLevel, batteryTemp, solarPower)
                        . receiveManagePowerEnd
                        . MainController(true, false, 0, speed)

                + sum newThrottle : Int . (newThrottle >= 1 && newThrottle <= MAX)
                ->  (
                        pressThrottle(newThrottle) . deactivateCruise
                        . sendManagePowerStart(newThrottle, speed, batteryLevel, batteryTemp, solarPower)
                        . receiveManagePowerEnd
                        . MainController(true, false, newThrottle, speed)

                    )

                + (cruise == false && throttle == 0 && speed != 0) -> activateCruise(speed) . MainController(true, true, 0, speed)
                + PowerOff . MainController(false, false, 0, 0)
                + PressBrake(speed, batteryLevel, batteryTemp, false) . MainController(true, false, 0, 0)
            )
        )

    <> pressOn . MainController(true, false, 0, 0);


proc PowerController =
    sum throttle : Int, speed : Int, batteryLevel : BatteryLevelStatus, batteryTemp : BatteryTempStatus, solarPower : Int 
    . receiveManagePowerStart(throttle, speed, batteryLevel, batteryTemp, solarPower)
    . setMotorRequirePower(throttle)
    .   
    (
        (throttle == 0)
        ->  ( 
                (batteryLevel == Full || batteryTemp == Overheating)
                ->  conSolarPanelToMotor(0) . conBatteryToMotor(0) . conMotorToBattery(0)
                <>  conSolarPanelToMotor(0) . conBatteryToMotor(0) . conMotorToBattery(speed)
            )
        <>  (
                (solarPower > throttle)
                ->  conMotorToBattery(0) . conBatteryToMotor(0) . conSolarPanelToMotor(throttle)
                    .
                    (batteryLevel == Full || batteryTemp == Overheating)
                    ->  conSolarPanelToBattery(0)
                    <>  conSolarPanelToBattery(solarPower - throttle)
                <>  conSolarPanelToBattery(0) . conMotorToBattery(0) . conSolarPanelToMotor(solarPower)
                    .
                    (batteryLevel == Empty)
                    ->  conBatteryToMotor(0)
                    <>  conBatteryToMotor(throttle - solarPower)
            )
    )
    . sendManagePowerEnd
    . PowerController;


proc CruiseStepController =
    sum throttle : Int, desiredSpeed : Int, currentSpeed : Int, batteryLevel : BatteryLevelStatus, batteryTemp : BatteryTempStatus, solarPower : Int
    . receiveCruiseStepStart(throttle, desiredSpeed, currentSpeed, batteryLevel, batteryTemp, solarPower)
    . 
    (
        (desiredSpeed - currentSpeed > SPEED_THRESHOLD && throttle < MAX)
        ->  sendManagePowerStart(throttle + 1, currentSpeed, batteryLevel, batteryTemp, solarPower) . receiveManagePowerEnd . sendCruiseStepEnd(throttle + 1)

        <>  (currentSpeed - desiredSpeed > SPEED_THRESHOLD && throttle > 0)
            ->  sendManagePowerStart(throttle - 1, currentSpeed, batteryLevel, batteryTemp, solarPower) 
                . receiveManagePowerEnd . sendCruiseStepEnd(throttle - 1)
            <>  sendManagePowerStart(throttle, currentSpeed, batteryLevel, batteryTemp, solarPower) 
                . receiveManagePowerEnd . sendCruiseStepEnd(throttle)
        ) . CruiseStepController;


proc DataStreamController = 
    receiveStreamDataStart
    . sum speed : Int . (speed >= 0 && speed <= MAX)
    ->  getSpeed(speed) . sendSpeed(speed)
        . sum batteryLevel : BatteryLevelStatus
            . getBatteryLevel(batteryLevel) . sendBatteryLevel(batteryLevel) 
                . sum batteryTemp : BatteryTempStatus
                    . getBatteryTemp(batteryTemp) . sendBatteryTemp(batteryTemp)
                        . sum solarPower : Int . (solarPower >= 0 && solarPower <= MAX)
                        ->  getSolarPower(solarPower) . sendSolarPower(solarPower)
                            . sendStreamDataEnd(speed, batteryLevel, batteryTemp, solarPower)
                            . DataStreamController;



% ---------------- Auxiliary Processes -------------- %

proc PowerOff = pressOff . conMotorToBattery(0) . conSolarPanelToMotor(0) . conSolarPanelToBattery(0) . conBatteryToMotor(0) . deactivateCruise;

proc PressBrake(speed : Int, batteryLevel : BatteryLevelStatus, batteryTemp : BatteryTempStatus, braking : Bool) = 
                (!braking)
                ->  pressBrake . deactivateCruise . conBatteryToMotor(0) . conSolarPanelToMotor(0) . activateBrakes
                    . 
                    (
                        (batteryLevel == Full || batteryTemp == Overheating)
                        ->  conMotorToBattery(0)
                        <>  conMotorToBattery(speed)
                    ) . PressBrake(speed, batteryLevel, batteryTemp, true)
                <>  releaseBrake . deactivateCruise . conMotorToBattery(0) . deactivateBrakes;


%------------------------- Initialization -------------------------------------%

init hide({continue},
  allow(
    {
        newCycle,
        pressOn, pressOff, pressThrottle, pressBrake, releaseBrake, activateCruise, deactivateCruise, emergencySignal,
        getBatteryLevel, getBatteryTemp, conBatteryToMotor, getSpeed, conMotorToBattery, setMotorRequirePower, getSolarPower,
        conSolarPanelToMotor, conSolarPanelToBattery, activateBrakes, deactivateBrakes, enterSafeMode, exitSafeMode,
        sendBatteryLevel, sendBatteryTemp, sendSolarPower, sendSpeed, startStreamData, endStreamData, startCruiseStep,
        endCruiseStep, startManagePower, endManagePower, releaseThrottle, continue, test, testN 

    },
    comm(
      {
        sendStreamDataStart     | receiveStreamDataStart    -> startStreamData,
        sendStreamDataEnd       | receiveStreamDataEnd      -> endStreamData,
        sendCruiseStepStart     | receiveCruiseStepStart    -> startCruiseStep,
        sendCruiseStepEnd       | receiveCruiseStepEnd      -> endCruiseStep,
        sendManagePowerStart    | receiveManagePowerStart   -> startManagePower,
        sendManagePowerEnd      | receiveManagePowerEnd     -> endManagePower
      },
      MainController(false, false, 0, 0) || CruiseStepController || DataStreamController || PowerController
  )));