%------------------------- Sort declarations -------------------------------------%


sort SolarCarState = struct On ? isOn | Off ? isOff;
sort BatteryLevelStatus = struct Empty ? isEmpty | Medium ? isMedium | Full ? isFull;
sort BatteryTempStatus  = struct Overheating ? isOverheating | Normal ? isNormal;

%------------------------- Map declarations -------------------------------------%

map MAX : Int;
eqn MAX = 10;

map SPEED_THRESHOLD : Int;
eqn SPEED_THRESHOLD = 3;


%------------------------- Action declarations -------------------------------------%

% Input actions

act pressOn, pressOff;
pressThrottle : Int;
act pressBrake, releaseBrake;

act activateCruise : Int;
act deactivateCruise;
act emergencySignal;

% Battery actions

act getBatteryLevel : BatteryLevelStatus;
act getBatteryTemp : BatteryTempStatus;
act conBatteryToMotor : Int;

% Motor actions

act getSpeed : Int;
act conMotorToBattery : Int;
act setMotorRequirePower : Int;

% Solar Panel actions
act getSolarPower : Int;
act conSolarPanelToMotor : Int;
act conSolarPanelToBattery : Int;

% Mechanical brakes

act activateBrakes, deactivateBrakes;

% External environment/external hazard

act enterSafeMode, exitSafeMode;

% Monitoring system

act sendBatteryLevel : BatteryLevelStatus;
act sendBatteryTemp : BatteryTempStatus;
act sendSolarPower : Int;
act sendSpeed : Int;

% Internal actions

act sendStreamDataStart, receiveStreamDataStart, startStreamData;
act sendStreamDataEnd, receiveStreamDataEnd, endStreamData : BatteryLevelStatus # BatteryTempStatus # Int # Int;

act sendCruiseControlStart, receiveCruiseControlStart, startCruiseControl : Int # Int;
act sendCruiseControlEnd, receiveCruiseControlEnd, endCruiseControl : Bool;

act continue;

act test : Bool;
act testN : Int;


%------------------------- Process declarations -------------------------------------%

%------------------------ MainController Processes --------------%

proc MainController(on : Bool) = 
	(on)
		-> 
 		(
			PowerOff . MainController(false)
			+ sum throttle : Int, speed : Int . (throttle >= 0 && throttle <= MAX && speed >= 0 && speed <= MAX)
			->  (
					ManageMotorPower(throttle, speed, false)
				 	. 	(
					 		% ActivateCruise(throttle, speed)
					 		sendCruiseControlStart(throttle, speed) . sum offPressed : Bool
					 		. receiveCruiseControlEnd(offPressed) . MainController(!offPressed)
					 		+ MainController(true)
				 		)
				)

			+ PressBrake . MainController(true)
			)
		<> pressOn . MainController(true);

proc ManageMotorPower(throttle : Int, speed : Int, cruise : Bool) = 
				sendStreamDataStart 
				. sum bl : BatteryLevelStatus, bt : BatteryTempStatus, sp : Int . receiveStreamDataEnd(bl, bt, sp, speed) .
				
				((cruise == false) 	
					-> pressThrottle(throttle) . setMotorRequirePower(throttle)
					<> setMotorRequirePower(throttle)
				)

				. 
				
				% regenerate energy
				(throttle == 0)
					->	((bl == Full || bt == Overheating)
								-> conMotorToBattery(0)
								<> conMotorToBattery(speed) % TODO change to something else
						)
					<> 	(conSolarPanelToMotor(min(sp, throttle))
						. 	((bl == Empty)
								-> conBatteryToMotor(0)
								<> conBatteryToMotor(max(0, throttle - sp)) . conSolarPanelToBattery(max(0, sp - throttle))
							)
						);


proc CruiseController(on : Bool, curThrottle : Int, desiredSpeed : Int, currentSpeed : Int) = 
	on	-> 	(
				PowerOff . DeactivateCruise(true)
				+ sum throttle : Int, speed : Int . (throttle >= 0 && throttle <= MAX && speed >= 0 && speed <= MAX)
					->  ManageMotorPower(throttle, speed, false) . DeactivateCruise(false)
				+ PressBrake . DeactivateCruise(false)

				+ Cruise(curThrottle, desiredSpeed, currentSpeed)
		   	)
		<>	(
				sum throttle : Int, speed : Int 
					. receiveCruiseControlStart(throttle, speed)
					. CruiseController(true, throttle, speed, speed)
			);

proc Cruise(throttle : Int, desiredSpeed : Int, currentSpeed : Int) =
									activateCruise(desiredSpeed) .
									sum nextCurrentSpeed : Int . (nextCurrentSpeed >= 0 && nextCurrentSpeed <= MAX)
										-> (desiredSpeed - currentSpeed > SPEED_THRESHOLD && throttle < MAX)
											-> ManageMotorPower(throttle + 1, nextCurrentSpeed, true) . CruiseController(true, throttle + 1, desiredSpeed, nextCurrentSpeed)

											<> (currentSpeed - desiredSpeed < SPEED_THRESHOLD && throttle > 0)
											 	-> ManageMotorPower(throttle - 1, nextCurrentSpeed, true) . CruiseController(true, throttle - 1, desiredSpeed, nextCurrentSpeed)
											 	<> ManageMotorPower(throttle, nextCurrentSpeed, true) . CruiseController(true, throttle, desiredSpeed, nextCurrentSpeed);

proc DataStreamController = receiveStreamDataStart
				. sum bl : BatteryLevelStatus
					. getBatteryLevel(bl) . sendBatteryLevel(bl) 
						. sum bt : BatteryTempStatus
							. getBatteryTemp(bt) . sendBatteryTemp(bt)
								. sum sp : Int . (sp >= 0 && sp <= MAX)
								->  getSolarPower(sp) . sendSolarPower(sp)
									. sum s : Int . (s >= 0 && s <= MAX)
									->  getSpeed(s) . sendSpeed(s)
										. sendStreamDataEnd(bl, bt, sp, s) . DataStreamController;



% ---------------- Auxiliary Processes -------------- %

proc PowerOff = pressOff . conMotorToBattery(0) . conSolarPanelToMotor(0) . conBatteryToMotor(0);


proc DeactivateCruise(offPressed : Bool) = deactivateCruise . sendCruiseControlEnd(offPressed) . CruiseController(false, 0, 0, 0);

proc ActivateCruise(throttle : Int, speed : Int) =
	 sendCruiseControlStart(throttle, speed) . sum offPressed : Bool . receiveCruiseControlEnd(offPressed) . MainController(!offPressed);

proc PressBrake = 
				pressBrake . conBatteryToMotor(0) . conSolarPanelToMotor(0) . sum currentSpeed : Int
				. (currentSpeed >= 0 && currentSpeed <= MAX) -> conMotorToBattery(currentSpeed) . activateBrakes
				+ releaseBrake . conMotorToBattery(0) . deactivateBrakes;


proc Test = sum n : Int . (n < 10) -> pressOn . pressThrottle(n) . Test;



%------------------------- Initialization -------------------------------------%

init
  allow(
    { 
    	pressOn, pressOff, pressThrottle, pressBrake, releaseBrake, activateCruise, deactivateCruise, emergencySignal,
    	getBatteryLevel, getBatteryTemp, conBatteryToMotor, getSpeed, conMotorToBattery, setMotorRequirePower, getSolarPower,
    	conSolarPanelToMotor, conSolarPanelToBattery, activateBrakes, deactivateBrakes, enterSafeMode, exitSafeMode,
    	sendBatteryLevel, sendBatteryTemp, sendSolarPower, sendSpeed, startStreamData, endStreamData, startCruiseControl,
    	endCruiseControl, continue, test, testN	

    },
    comm(
      {
      	sendStreamDataStart | receiveStreamDataStart -> startStreamData,
      	sendStreamDataEnd | receiveStreamDataEnd -> endStreamData,
      	sendCruiseControlStart | receiveCruiseControlStart -> startCruiseControl,
      	sendCruiseControlEnd | receiveCruiseControlEnd -> endCruiseControl
      },
      MainController(false) || CruiseController(false, 0, 0, 0) || DataStreamController
  ));
